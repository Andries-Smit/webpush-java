group 'nl.martijndwars'
version '2.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    baseName = 'web-push'
    version = '2.0.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      pom.project {
        name 'web-push'
        packaging 'jar'
        description 'A Web Push library for Java.'
        url 'https://github.com/MartijnDwars/web-push'

        scm {
          connection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          developerConnection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          url 'git@github.com:MartijnDwars/web-push.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'martijndwars'
            name 'Martijn Dwars'
            email 'ikben@martijndwars.nl'
          }
        }
      }
    }
  }
}

repositories {
    mavenCentral()
}

dependencies {
    // For making HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'

    // For turning InputStream to String
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    // Not sure what for..
    compile group: 'com.google.guava', name: 'guava', version: '19.0'

    // For cryptographic operations
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'

    // For encoding JSON
    compile group: 'org.json', name: 'json', version: '20160212'

    // For creating and signing JWT
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.5.2'
    
    testCompile group: 'junit', name: 'junit', version: '4.11'

    testCompile 'org.eclipse.jetty:jetty-server:9.4.0.M1'

    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.53.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version: '2.53.1'

    // Selenium 2.53 needs the Marionette driver. WebDriverManager downloads and makes it available.
    // See https://github.com/SeleniumHQ/selenium/issues/1862
    testCompile 'io.github.bonigarcia:webdrivermanager:1.4.9'
}
