group 'nl.martijndwars'
version '2.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'org.junit.platform.gradle.plugin'

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
    sourceCompatibility = 1.8
}

jar {
    baseName = 'web-push'
    version = '2.0.0'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(
          userName: project.hasProperty('ossrhUsername') ? ossrhUsername : '',
          password: project.hasProperty('ossrhPassword') ? ossrhPassword : ''
        )
      }

      pom.project {
        name 'web-push'
        packaging 'jar'
        description 'A Web Push library for Java.'
        url 'https://github.com/MartijnDwars/web-push'

        scm {
          connection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          developerConnection 'scm:git:git@github.com:MartijnDwars/web-push.git'
          url 'git@github.com:MartijnDwars/web-push.git'
        }

        licenses {
          license {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
          }
        }

        developers {
          developer {
            id 'martijndwars'
            name 'Martijn Dwars'
            email 'ikben@martijndwars.nl'
          }
        }
      }
    }
  }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // For making HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'

    // For making async HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'

    // Not sure what for..
    compile group: 'com.google.guava', name: 'guava', version: '19.0'

    // For cryptographic operations
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'

    // For creating and signing JWT
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.5.2'


    // For testing, obviously
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.0.0-M4'

    // For running JUnit tests
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.0.0-M4'

    // For turning InputStream to String
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'

    // For running the demo web-app that is used for Selenium tests
    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.0.M1'

    // For reading the demo vapid keypair from a pem file
    testCompile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.55'

    // For running Selenium tests
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.3.1'

    // For downloading and linking WebDriver's for various browsers
    testCompile group: 'io.github.bonigarcia', name: 'webdrivermanager', version: '1.4.9'

    // For constructing JSON
    testCompile group: 'org.json', name: 'json', version: '20160810'

    // For parsing JSON
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
}
